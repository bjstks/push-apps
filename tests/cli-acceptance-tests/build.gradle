apply from: "$rootProject.rootDir/gradle/kotlin.gradle"
apply from: "$rootProject.rootDir/gradle/version.gradle"
apply from: "$rootProject.rootDir/gradle/idea.gradle"

dependencies {
    testCompile project(":components:push-apps")
    testCompile project(":applications:cli")

    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testCompile "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
    testCompile "org.junit.platform:junit-platform-console-standalone:$junitPlatformVersion"
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'

    testCompile 'org.apache.commons:commons-io:1.3.2'
    testCompile 'com.github.kittinunf.fuel:fuel:1.11.0'
    testCompile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2'
}

task buildHelloApp(type: Exec) {
    environment GOOS: "linux", GOPATH: "${System.getenv("HOME")}/workspace/go"
    workingDir "${project.projectDir}/src/test/kotlin/support/helloapp"
    commandLine "go", "build", "-o", "helloapp"
}

task packageHelloApp(type: Zip) {
    dependsOn "buildHelloApp"

    from "${project.projectDir}/src/test/kotlin/support/helloapp"
    include "*"
    archiveName "helloapp.zip"
    destinationDir file("${project.projectDir}/src/test/kotlin/support")
}

task buildGoodbyeApp(type: Exec) {
    environment GOOS: "linux", GOPATH: "${System.getenv("HOME")}/workspace/go"
    workingDir "${project.projectDir}/src/test/kotlin/support/goodbyeapp"
    commandLine "go", "build", "-o", "goodbyeapp"
}

task packageGoodbyeApp(type: Zip) {
    dependsOn "buildGoodbyeApp"

    from "${project.projectDir}/src/test/kotlin/support/goodbyeapp"
    include "*"
    archiveName "goodbyeapp.zip"
    destinationDir file("${project.projectDir}/src/test/kotlin/support")
}

task buildSampleApps {
    dependsOn "packageHelloApp", "packageGoodbyeApp"
}

task acceptanceTest(
        type: JavaExec,
        description: 'Runs acceptance tests.',
        group: 'Verification'
) {
    dependsOn ":applications:cli:assemble"
    dependsOn 'testClasses'
    dependsOn "buildSampleApps"

    environment([
            "PUSHAPPS_VERSION": "$version"
    ])

    classpath = sourceSets.test.runtimeClasspath

    main = 'org.junit.platform.console.ConsoleLauncher'
    args = ["-p", "acceptance", "-e", "spek"]
}
