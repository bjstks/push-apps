buildscript {
    ext.kotlinVersion = '1.1.51'
    ext.junitPlatformVersion = '1.0.1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.1.RELEASE"
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'
apply from: "$rootProject.rootDir/version.gradle"
apply from: "$rootProject.rootDir/test.gradle"

mainClassName = 'io.pivotal.pushapps.PushApps'
jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }

    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself. One can extend this task, to skip certain
    // files or particular types at will
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://repo.spring.io/snapshot'
    }
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile 'com.beust:jcommander:1.72'
    compile 'org.cloudfoundry:cloudfoundry-client-reactor:2.13.0.RELEASE'
    compile 'org.cloudfoundry:cloudfoundry-operations:2.13.0.RELEASE'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'
    compile 'org.apache.logging.log4j:log4j-core:2.9.1'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.7.1-2'
    compile 'org.flywaydb:flyway-core:4.2.0'
    compile 'mysql:mysql-connector-java:5.1.6'
    compile 'org.postgresql:postgresql:42.1.4'

    compile 'io.projectreactor:reactor-core'
    compile 'io.projectreactor.ipc:reactor-netty'

    testCompile "io.damo.aspen:aspen:2.0.0"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testCompile "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
    testCompile "org.junit.platform:junit-platform-console-standalone:$junitPlatformVersion"
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}

task version {
    println "$version"
}

task buildHelloApp(type: Exec) {
    environment GOOS: "linux", GOPATH: "${System.getenv("HOME")}/workspace/go"
    workingDir "${project.rootDir}/src/test/kotlin/support/helloapp"
    commandLine "go", "build", "-o", "helloapp"
}

task packageHelloApp(type: Zip) {
    dependsOn "buildHelloApp"

    from "${project.rootDir}/src/test/kotlin/support/helloapp"
    include "*"
    archiveName "helloapp.zip"
    destinationDir file("${project.rootDir}/src/test/kotlin/support")
}

task buildGoodbyeApp(type: Exec) {
    environment GOOS: "linux", GOPATH: "${System.getenv("HOME")}/workspace/go"
    workingDir "${project.rootDir}/src/test/kotlin/support/goodbyeapp"
    commandLine "go", "build", "-o", "goodbyeapp"
}

task packageGoodbyeApp(type: Zip) {
    dependsOn "buildGoodbyeApp"

    from "${project.rootDir}/src/test/kotlin/support/goodbyeapp"
    include "*"
    archiveName "goodbyeapp.zip"
    destinationDir file("${project.rootDir}/src/test/kotlin/support")
}

task buildSampleApps {
    dependsOn "packageHelloApp", "packageGoodbyeApp"
}

task bumpPatch(type: UpdatePatchVersionTask) {
    versionFilePath = "$rootProject.rootDir/version.gradle"
}

class UpdatePatchVersionTask extends DefaultTask {
    @Input
    String versionFilePath

    @TaskAction
    def updatePatchVersion() {
        def versionFile = new File(versionFilePath)
        def currentVersion = versionFile.text
        def versionStringParts = currentVersion.split("\\.")
        def patchInt = Integer.parseInt(versionStringParts.last().replace("'\n", ""))
        versionStringParts[versionStringParts.size() - 1] = (patchInt + 1).toString() + "'\n"

        versionFile.text = versionStringParts.join(".")
    }
}
